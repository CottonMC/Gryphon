buildscript {
	repositories {
		jcenter()
		maven {
			name = 'Fabric'
			url = 'http://maven.modmuss50.me/'
		}
	}
	dependencies {
		classpath "net.fabricmc:fabric-loom:0.2.1-SNAPSHOT"
	}
}

plugins {
	id 'java'
	id 'eclipse'
	id 'idea'
}

apply plugin: net.fabricmc.loom.LoomGradlePlugin

sourceCompatibility = 1.8
targetCompatibility = 1.8

apply from: "gryphon.gradle"

archivesBaseName = project.ext.projectName
version = project.ext.version

minecraft {
	refmapName = 'mixins.gryphon.refmap.json'
}

repositories {
	mavenCentral()
	maven { url "http://server.bbkr.space:8081/artifactory/libs-snapshit" }
	maven { url "https://minecraft.curseforge.com/api/maven" }
	flatDir { dirs 'libs' }
}

dependencies {
	minecraft "com.mojang:minecraft:1.14 Pre-Release 2"
	mappings "net.fabricmc:yarn:1.14 Pre-Release 2+build.1"
	modCompile "net.fabricmc:fabric-loader:0.4.1+build.123"
	for (String mod in project.ext.mavenMods) {
		modCompile mod
		include mod
	}
	for (String mod in project.ext.fileMods) {
		modCompile name: mod+"-dev"
		include name: mod
	}
}

processResources {
	inputs.property "version", project.version
	inputs.property "name", project.ext.name
	inputs.property "description", project.ext.description
	from(sourceSets.main.resources.srcDirs) {
		include "fabric.mod.json"
		expand version: project.version
		expand name: project.ext.name
		expand description: project.ext.description
	}
	from(sourceSets.main.resources.srcDirs) {
		exclude "fabric.mod.json"
	}
}
